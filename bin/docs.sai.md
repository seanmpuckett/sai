#!/usr/bin/env sai-run

# DOCS.sai.md

Generates the `20.references.md` file from compiler introspection.

_Note: the resulting file is put in the current working directory._


### Globals

    reference:
      SAI from require 'sai-language'
      FS from require 'fs'
      PATH from require 'path'


## Docs object

Declared as `main` for automatic instantiation on compilation.
  
    object Docs main

### Static attributes

    given:

#### .categories 

Descriptions of the different categories.

      categories:
        reference '''
          Full reference documentation.
            
        constructs '''
          One-or-multi line constructs used for looping, flow control, error handling, and so on.
      
        pipers '''
          Syntactical elements starting with the pipe character `|` that modify values in-line
      
        keywords '''
          Low-level language syntax.
      
        pronouns '''
          Context-aware, renamable variables.
          
        globals '''
          Functions and values avalilable, unscoped, in the global namespace.
          
  
### Instantiate task
  
    Instantiate task
    
      set docs from SAI.Document
      set master blank
      set index blank
      set header ''


      each docs.documents as pages, c
        each pages as page, name
          set 
            page.category ? c
            category page.category
            pageName to master\name ?? '${name} (${category})' :: name
            master\pageName page
            index\category ? blank
            index\category\name ' - __${page.title ? name}__ - ${page.subtitle}\n'
      
  
      each docs.references as alsos, see
        set master\see default:
          title toUpperCase'd see
          reference '''
            _See: ${(alsos | thru .1 ).join(', ')}_

        ply alsos as also
          set index[also.0] ? blank
          set index[also.0][see] default ' - __${also.2}__ _(see: ${(alsos | thru .1).join(', ')})_\n'

  
      set header to chain index
        | enlist | by IndexSort(.0)
        | thru '''
          ### ${toUpperCase'd .0}
      
          ${.1 | by IndexSort(key) | total}


        | total
    
    
      set header to '''
         ## Index by category
     
         ${header}
     
         ## Encyclopedia
    
      Write:
         filename "20.Reference.md"
         pages master
         category 'reference'
         header header
  
      debug "Wrote docs"


    Indent task as spaces, text
      return chain text
        split /[\r\n]/g
        |thru ' '.repeat(spaces) + it
        join '\n'

    LineTrim task as trailing, text
      set lines split'd text /[\r\n]/g
      while ( lines | first ) is ''
        shift'd lines
      while ( lines | last ) is ''
        pop'd lines
      count trailing
        push'd lines ''
      return join'd lines '\n'

    IndexSort unbound task as a
      set a toUpperCase'd self
      set ch from a.charCodeAt 0
      if ch>=65 and ch<=90
        set ch + 200000
      else
        set ch + 100000
      return ch+a
    


    Write task expects $filename, $pages, $category, $header

      set output from LineTrim 3, '''
        # ${toUpperCase'd $category}
    
        ${from LineTrim 0, categories\$category ? 'Unknown Heading'}

        ### How to use this documentation
    
        The categorized index at the top provides a quick overview of each language element. To learn
        more about a particular entry, search the full reference document for `^name`, where _name_ is the 
        symbol or word of interest.

        This file is automatically generated. _Do not edit._
    
    
        ${from LineTrim 0, $header}
    
      set content to empty
  
  
      index $pages, ( $pages | keys | by IndexSort(it) )
        if .reference
          push'd content from LineTrim 4, '''
            ### ${.title}
        
            ${from LineTrim 0, .reference}
        
        
        else
          set aux  .category is 'pipers' ?? '| ' :: ''
          push'd content from LineTrim 4, '''
            ### ${aux}${.title ? .name.toUpperCase()} - _${.subtitle}_ - ${.category} - ^${.title ? .name} 

            ${from LineTrim 1, .summary}      
            ${from LineTrim 1, from Indent 4, .synref ? 'synref'}
            ${from LineTrim 1, .detail}
  
      set output + join'd content ''

      FS.writeFileSync $filename, output
  
  
    
    
  
  
  
  