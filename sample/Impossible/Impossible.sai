// X and Y are two different integers, greater than 1, with sum less than 100. S and P are two mathematicians; S knows the sum X+Y, P knows the product X*Y, and both know the information in these two sentences. The following conversation occurs.
//
// 1) P says "I cannot find these numbers."
// 2) S says "I was sure that you could not find them. I cannot find them either."
// 3) P says "Then, I found these numbers."
// 4) S says "If you could find them, then I also found them."

// What are these numbers?

reference:
  MAX 100

object Impossible 1.0.0

Instantiate task

  set pairs to process
    count 1 to ~MAX as x
      count 1 to ~MAX as y
        if 1<x and x<y and x+y<100
          yield: x x, y y, s x+y, p x*y

  set PGood to empty
  set SBad to empty
  set PStillGood to empty
  set SGood to empty
  
  debug "Mathemetician's \"Impossible\" puzzle solver."
  debug chain pairs() reap
    thru 
      set PGood[.p] to (self default 0)+1
    thru 
      if PGood[.p]=1
        set SBad[.s] to true
    has not SBad[.s] 
    thru 
      set PStillGood[.p] to (self default 0)+1
    has PStillGood[.p]=1
    thru 
      set SGood[.s] to (self default 0)+1
    has SGood[.s]=1
    thru 'Solution ${key+1}: X=${.x}, Y=${.y}; sum=${.s}, product=${.p}'
    join '\n'

