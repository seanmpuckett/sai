
object MiscC singleton

inherit: Construct

// MANIFEST

manifest get 
  return:
    'constructs':
      'orphan':
        repeats false
        clauses:
          'orphan': #none
        handler bind MiscConstruct
      
      'with':
        repeats false
        clauses:
          'with': #exproutblock
        handler bind MiscConstruct

      'inc':
        repeats false
        clauses:
          'inc': #rvalue
        handler bind MiscConstruct

      'dec':
        repeats false
        clauses:
          'dec': #rvalue
        handler bind MiscConstruct

      'delete':
        repeats false
        clauses:
          'delete': #rvalue
        handler bind MiscConstruct
      


docs get 
  return:
    'constructs':

      'dec':
        category 'keywords'
        subtitle 'decrement verb'
        summary '''
          Decrements (reduces by 1) the value in the given variable.
        synref '''
          dec [var]
        detail '''
          Example:

            set a to 2
            dec a
            debug a
  
            > 1


      'delete':
        category 'keywords'
        subtitle 'a verb that deletes variables'
        summary '''
          summary
        synref '''
          delete [variable]
        detail '''
          When called on a variable or traits, undefines the named variable or attribute(s) of a variable.

              delete thisUnwantedValue
              delete goodObject.unwantedAttribute

          This just calls the underlying Javascript version of **delete**, with all the same rules and caveats.
          
          #### delete pipe operator
          
          The __delete__ pipe operator has advanced functionality, which see.


      'inc':
        category 'keywords'
        subtitle 'value increment'
        summary '''
          Increments (adds 1 to) the given variable.
        synref '''
          inc [var]
        detail '''
          As in:
          
              set a to 1
              inc a
              debug a
  
              > 2

          The __inc__ statement has special behaviour around values that are _undefined_ or evaulate to _falsy_; when you increment one of these values, it becomes `1`.  For example:

              set a to undefined
              inc a
              debug a
    
              > 1
    
          This is useful when tallying with an array, e.g. the following construct automatically initializes array elements the way the standard `++` operator does not, such that the `tally` object will contain a log of word usage.

              set tally blank
              every word
                inc tally\it
      
          Note that __dec__ does not have similar initialization functionality.


    
    
      'orphan':
        category 'keywords'
        subtitle 'rescoping this'
        summary '''
          **Orphan** causes the __@__ scoping prefix within the function to refer to the context 
          of the function call, instead of the context of the function definition. 
        detail '''
          You probably want to use this in combination with **local** variable definitions.

          For clarifications and example code on this, see **task**.

          To do this on a case-by-case basis, use the __@@__ scoping prefix (which see).


      'with':
        subtitle 'block-level pronoun assignment'
        summary '''
          Allows the use of the **it** pronoun (and the `.` scoping prefix) within an arbitrary block of code. 
        detail '''
          For example:

              with customer
                set label to '''
                  $\{.name}
                  $\{.address1}
                  $\{.address2}
                  $\{.city} $\{.region} $\{.postcode}

          Another example, this time using an assigned pronoun:
          
              with category\\document\\page as p
                set precis:
                  title p.title
                  authors join!p.authors ', '
                  summary p.abstract ? p.summary
                  
          Though that is much the same as this:
          
              local p category\\document\\page
              set precis:
                title p.title
                authors join!p.authors ', '
                summary p.abstract ? p.summary
                
          __WITH__ can make things tidier, but mind what your pronouns are.



MiscConstruct task

  if $clauses.length > 1
    return: error 'SAI bug: error parsing construct with multiple clauses: ${join!($clauses | keys) ', '}'

  set body ''

  // args.exprs.0 = 'array' / 'fields'
  // args.exprs.1 = insertable text
  // args.exprs.2 = heirarchical structure of elements

  with $clauses.0
  
    switch .type
    case 'orphan.none'
      set body 'var $=this;\n'

    case 'inc.rvalue'
      set body '${.args.rvalue}=(${.args.rvalue} || 0)+1;\n'
      
    case 'dec.rvalue'
      set body '${.args.rvalue}--;\n'
        
    case 'delete.rvalue'
      set body 'delete ${.args.rvalue};\n'

    case 'with.exproutblock'
      set
        outs !@OutVarHelper .args.out      
        vars:
          'this' outs.0
          'block' .args.block
          'expr' .args.expr
        body '''
          var ^{this}=^{expr};
          ^{block}; 
        body !$tools.replacer body, vars  

    else
      return: error "Unhandled construct type ${.type} in ReturnConstruct"
      
  return: body body


