
object QueryP singleton

inherit: Construct

given:
  SPLIT '^&*SP.L.I.' + ~Math.random() + 'T.H.E.RE*&^'

// MANIFEST

manifest get 
  return:
  
    'pipers':

      'limit':
        clauses:
          'limit': #exprs
        handler bind LimitPiper

      'first':
        clauses:
          'first': #none, #expr, #outblock, #use
        handler bind FilterPiper

      'last':
        clauses:
          'last': #none, #expr, #outblock, #use
        handler bind FilterPiper

      'count':
        clauses:
          'count': #none
        handler bind LimitPiper

      'by':
        clauses:
          'by': #expr, #outblock, #use, #none
          'asc': #none
          'desc': #none
        handler bind SortPiper

      'any':
        clauses:
          'any': #expr
        handler bind InPiper

      'all':
        clauses:
          'all': #expr
        handler bind InPiper

      'has':
        clauses:
          'has': #expr, #outblock, #use
        handler bind FilterPiper
        
      'lowest':
        clauses:
          'lowest': #expr, #use, #none, #outblock
        handler bind LowestPiper
        
      'highest':
        clauses:
          'highest': #expr, #use, #none, #outblock
        handler bind LowestPiper
        

docs get
  return !Documents

//// FILTER PIPER 

FilterPiper task expects $clauses, $tools

  set
    pattern '(${SPLIT})'
    tools empty
    work $clauses.0
    operator 'filter_op'
    last false
    
  if $clauses.length > 1
     return: error "SAI Compiler: ${work.clause} should not have multiple clauses."

  if work.clause is 'first' or work.clause is 'last'
    set operator 'find_op'
  if work.clause is 'last'
    set last true

  switch work.type
  case 'has.expr', 'first.expr', 'last.expr'
    set 
      vars:
        'this' false
        'key' false
        'block' work.args.expr
      fn '''
        function(^{this},^{key}){ 
          return ^{block}; 
        }
      fn !$tools.replacer fn, vars
      pattern '$AI.${operator}(${SPLIT},${fn},${last})'

  case 'has.outblock', 'first.outblock', 'last.outblock'
    set
      outs !@OutVarHelper work.args.out      
      vars:
        'this' outs.0
        'key' outs.1
        'block' work.args.block
      fn '''
        function(^{this},^{key}){ 
          ^{block}; 
          return ^{this}; 
        }
      fn !$tools.replacer fn, vars  
      pattern '$AI.${operator}(${SPLIT},${fn},${last})'

  case 'has.use', 'first.use', 'last.use'
    set pattern '$AI.${operator}(${SPLIT},${work.args.use},${last})'

  case 'first.none'
    $tools.addlocal '$_b'
    set pattern '(Array.isArray($_b=${SPLIT}) ? $_b[0] : $AI.element_op($AI.slice_op($_b,0,1),0))'

  case 'last.none'
    $tools.addlocal '$_b'
    set pattern '(Array.isArray($_b=${SPLIT}) ? $_b[$_b.length-1] : $AI.element_op($AI.slice_op($_b,undefined,-1),0))'

  else
    return: error "Unhandled piper type ${trial} in FilterPiper"
      
  return: leftright pattern.split('${SPLIT}'), tools tools




//// ANY/ALL (IN) PIPER 

InPiper task expects $clauses, $tools

  set
    pattern '(${SPLIT})'
    tools empty
    Repl $tools.replacer
    operator 'or'
    work $clauses.0
    
  if $clauses.length > 1
     return: error "SAI Compiler: ${work.clause} should not have multiple clauses."

  if work.clause is 'all'
    set operator to 'and'

  switch work.type

  case 'any.expr', 'all.expr'
    set 
      pattern '$AI.in_op(${SPLIT},${work.args.expr},"${operator}")'

  else
    return: error "Unhandled piper type ${trial} in InPiper"

  return: leftright pattern.split('${SPLIT}'), tools tools



//// LOWEST/HIGHEST PIPER 

LowestPiper task expects $clauses, $tools


  set
    pattern '(${SPLIT})'
    tools empty
    Repl $tools.replacer
    ascending 1
    work $clauses.0
    
  if $clauses.length > 1
     return: error "SAI Compiler: ${work.clause} should not have multiple clauses."

  if work.clause is 'highest'
    set ascending to -1

  switch work.type

  case 'lowest.expr', 'highest.expr'
    set 
      afetch !Repl work.args.expr, fields 'this' 'a'
      bfetch !Repl work.args.expr, fields 'this' 'b'
      whichway to ascending>0 ?? 'aa>bb' :: 'aa<bb'

      fn '''
        function(a,b){ 
          var aa=${afetch};
          var bb=${bfetch};
          return (${whichway}) ? b : a;
        }

      pattern '$AI.reduce_op(${SPLIT},${fn})'

  case 'lowest.outblock', 'highest.outblock'
    set
      outs !@OutVarHelper work.args.out      
      whichway to ascending>0 ?? '>0' :: '<=0'
      vars:
        'a' outs.0 ? '_a'
        'b' outs.1 ? '_b'
        'block' work.args.block
      fn '''
        function(^{a},^{b}){ 
          ^{block}; 
        }
      fn !$tools.replacer fn, vars  
      pattern '''
        $AI.reduce_op(${SPLIT},function(a,b){
          return (${fn}(a,b)${whichway}) ? b : a;
        })

  case 'lowest.use', 'highest.use'
    set
      whichway to ascending>0 ?? '>0' :: '<=0'
      pattern '''
        $AI.reduce_op(${SPLIT},function(a,b){
          return (${work.args.use}(a,b)${whichway}) ? b : a;
        })

  case 'lowest.none', 'highest.none'
    set
      whichway to ascending>0 ?? 'a>b' :: 'a<b'
      pattern '''
        $AI.reduce_op(${SPLIT},function(a,b){
          return (${whichway}) ? b : a;
        })

  else
    return: error "Unhandled piper type ${trial} in LowestPiper"

  return: leftright pattern.split('${SPLIT}'), tools tools



//// SORT PIPER 

SortPiper task expects $clauses, $tools


  set
    pattern '(${SPLIT})'
    tools empty
    Repl $tools.replacer
    grammar $clauses | total (.clause)
    work $clauses.0
    
  switch work.type

  case 'by.expr'
    try
      set
        keyed false
        pattern to chain $clauses
          | into empty 
            switch .clause
            case 'by'
              unless keyed 
                set keyed ( .args.expr.indexOf('^{key}') >= 0 ) 
              push!sum: 
                expr ( .args.expr ? '^{this}' )
                order 1
            case 'asc'
              set (sum|last).order 1
            case 'desc'
              set (sum|last).order to -1
            else
              throw "SAI Compiler: unexpected clause in BY: ${trial}"
          | thru '''
            aa=${from Repl .expr, fields 'this' keyed??'a[1]'::'a', 'key' 'a[0]'};
            bb=${from Repl .expr, fields 'this' keyed??'b[1]'::'b', 'key' 'b[0]'};
            if (aa<bb) return ${-.order}; if (aa>bb) return ${.order};
          | total
          | set 
            unless keyed .. return '''
              $AI.sort_op(${SPLIT},function(a,b){
                var aa,bb;
                ${it};
                return 0;
              })
            else .. return '''
              $AI.map_op($AI.sort_op($by$enlistcheck(${SPLIT}),function(a,b){
                var aa,bb;
                ${it};
                return 0;
              }),function(a,b){return a[1];})
    catch as msg
      return: error msg

    if keyed
      push!tools '''
        var $by$enlistcheck=function(o) {
          if ($AI.isArray(o) || $AI.mustIterate(o)) {
            throw new Error("SAI Runtime: call to BY using the key pronoun applied to something that isn't a true object.");
          }
          return $AI.enlist_op(o);
        }
        
  case 'by.outblock'
    if $clauses.length > 1
      return: error "SAI Compiler: BY.outblock should not have any extra clauses"
      
    set
      outs !@OutVarHelper work.args.out      
      vars:
        'a' outs.0 ? '_a'
        'b' outs.1 ? '_b'
        'block' work.args.block
      fn '''
        function(^{a},^{b}){ 
          ^{block}; 
        }
      fn !$tools.replacer fn, vars  
      pattern '$AI.sort_op(${SPLIT},${fn})'

  case 'by.use'
    if $clauses.length > 1
      return: error "SAI Compiler: BY.use should not have any extra clauses"
    set pattern '$AI.sort_op(${SPLIT},${work.args.use})'

  case 'by.none'
    if $clauses.length > 1
      return: error "SAI Compiler: BY with no sort expression should not have any extra clauses. If you wish to sort descending, use by it desc."
    set 
      pattern '''
        $AI.sort_op(${SPLIT},function(a,b){return $AI.compare_op(a,b);})
    
  else
    return: error "Unhandled piper type ${trial} in SortPiper"

  return: leftright pattern.split('${SPLIT}'), tools tools



        

//// LIMIT PIPER 

LimitPiper task expects $clauses, $tools

  set 
    pattern '(${SPLIT})'
    tools empty
    work $clauses.0
    
  if $clauses.length > 1
     return: error "SAI Compiler: ${work.clause} should not have multiple clauses."
    
  switch work.type
  case 'limit.exprs'
    if work.args.exprs.length is 1
      set
        st undefined
        ct work.args.exprs.0
    else
      set
        st work.args.exprs.0
        ct work.args.exprs.1
      
    set pattern '$AI.slice_op(${SPLIT},${st},${ct})'

  case 'count.none'
    set pattern '$AI.count_op(${SPLIT})'

  else
    return: error "Unhandled piper type ${trial} in LimitPiper"
      
  return: leftright pattern.split('${SPLIT}'), tools tools







Documents task
  return:
    'pipers':
    
      'limit':
        subtitle 'select certain array/iterator elements'
        summary '''
          Returns a subset of elements from an array, string, or iterator.
        detail '''
          If one parameter is provided, returns the first **qty** elements in the 
          collection if **qty** is positive. If negative, returns the last (absolute) **qty** 
          number of elements. 
          
          If an **index** is supplied, return **qty** elements starting at **index**.  
          Negative index values do interesting things, there's a chart below.

            .. [collection] | limit [qty]
            .. [collection] | limit [index], [qty]

            .. [string] | limit [character count]
            .. [string] | limit [start character], [character count]

          You always get a list back, even if just one element will be returned.

              limit -y            last y rows
              limit 0             empty
              limit undefined     everything
              limit +y            first y rows
    
              limit -x, -y        everything except last y rows starting x from end of list
              limit -x, 0         empty
              limit -x, undef     last x rows
              limit -x, +y        y rows starting x from end of list
        
              limit 0, -y         everything except last y rows
              limit 0, 0          empty
              limit 0, undef      everything
              limit 0, +y         first y rows
 
              limit +x, -y        everything except last y rows starting at x
              limit +x, 0         empty
              limit +x, undef     everything starting at x
              limit +x, +y        y rows starting at x

          If the collection is an iterable, it will only be fetched as many times as needed. 
          (If you ask for elements offset from the end, the iterator will be exhausted because an 
          iterator’s length can only be ascertained by exhausting it.)

          If the iterator is infinite and you ask for an offset from the end, you'll run out of 
          memory as it will cache an infinite number of intermediate results order to comply 
          with your impossible request. (Infinite iterators have no end.)

          #### limit with strings

          You can use __limit__ to extract substrings. For this purpose characters are 
          treated as elements in the array.

              debug 'abcdef' | limit 2,2      // cd
              debug 'abcdef' | limit -3       // def
              debug 'abcdef' | limit -3,2     // de
              debug 'abcdef' | limit 1,-1     // bcde

          And so on. One "gotcha" is operator precedence; pipers have the lowest 
          priority, so you will need to wrap them in parenthesis if using __limit__ in any 
          logical expression.

              Do this: 
                if '#!' is (line | limit 0,2)   // correct code
    
              Not this:
                if '#!' is line | limit 0,2     // bad code
                if ('#!' is line) | limit 0,2   // also bad

          Another "gotcha" is the way  __limit__ intentionally changes behaviour when its 
          arguments change from negative to positive. If you pass calculated values that 
          could become negative, __limit__ may return things you don't expect. 


      'all':
        subtitle 'query a collection for all values'
        summary '''
          Checks a collection to see if it has _all_ of multiple values. If so, returns true.
        detail '''
          The expression to the left is the _haystack_, it is where we are looking. The expression on the right is the
          (set of) _needles_ that we are looking for.
          
          The __all__ piper examines the haystack to see if it has _all_ of the needles. If so, it returns __true__.
          Otherwise, it returns __undefined__.
          
          Example:
          
              set menu list tortilla, bean, lettuce, rice, meat, sauce

              debug menu | any list tofu, avocado       // undefined
              debug menu | any list fish, meat          // undefined
              debug menu | any list rice, bean          // true
              
          You can also test against a single value:
          
              debug menu | any 'bean'                   // 'bean'
              debug menu | any 'gelato'                 // undefined

          If there are duplicate _needles_, e.g. you query for `1, 2, 2, 3`; there must be at least that many
          duplicates in the haystack. The example below returns `undefined` because `bean` does not
          appear twice in the menu.
          
              debug menu | any list rice, bean, bean    // undefined

          #### Quirks

          You can use iterators on either side. Iterators on the needle side are collected, so don't try to
          needle with an infinite generator. 
          
          When the haystack is an object, its keys are examined, not values. 
          
          Using an object (or any scalar value) as the needle creates a test for _that object/value_.
          as if you had placed the object/value in an array by itself.
          
          No optimizations are made that require assumptions about the sortability of either haystack or
          needles, so this function is slower at the expense of being robust. If none of the needles are
          found in the haystack, every needle has been compared with every item in the haystack. It might
          be faster in some cases to sort and search, but this function does not do that.

          
      'any':
        subtitle 'query a collection for any of one-or-more values'
        summary '''
          Checks a collection to see if it has _any_ of multiple values. If so, returns the value it found first.
        detail '''
          The expression to the left is the _haystack_, it is where we are looking. The expression on the right is the
          (set of) _needles_ that we are looking for.
          
          The __any__ piper examines the haystack to see if it has _any_ of the needles. If so, it returns the first
          one it finds. Otherwise, it returns __undefined__.
          
          Example:
          
              set menu list tortilla, bean, lettuce, rice, meat, sauce

              debug menu | any list tofu, avocado       // undefined
              debug menu | any list fish, meat          // 'meat'
              debug menu | any list rice, bean          // 'rice'
              
          You can also test against a single value:
          
              debug menu | any 'bean'                   // 'bean'
              debug menu | any 'gelato'                 // undefined

          #### Quirks

          You can use iterators on either side. Iterators on the haystack side are collected, so don't try to
          query with an infinite generator. 
          
          When the haystack is an object, its keys are examined, not values. 
          
          Using an object (or any scalar value) as the needle creates a test for _that object_ 
          as if you had placed the object/value in an array by itself.

          No optimizations are made that require assumptions about the sortability of either haystack or
          needles, so this function is slower at the expense of being robust. If none of the needles are
          found in the haystack, every needle has been compared with every item in the haystack. It might
          be faster in some cases to sort and search, but this function does not do that.


      'first':
        subtitle 'first value'
        summary '''
          Returns the first value in a list or iterator. With an expression, block or function, returns the
          first value where the expression is true.
        detail '''
          Example:
          
              debug fruit | first
              > Apple

          __First__ doesn't just crop the array, it unwraps whatever value is there, as 
          if you'd coded `array.[0]`. __First__ unwraps iterators, returning a single
          value (or undefined). If you need the iteration chain preserved, use __limit__ (in combination with
          __has__ if necessary).
          
          #### First with a comparison
          
          If you provide a comparison expression, block, or function, __first__ will evaluate the comparison
          against each element in the collection, and return the first value that the comparison returns true.
          If you use __first__ on an object, the order of comparisons is undefined.

          Example:
          
              set fruit list Apple, Banana, Cherry, Durian
          
              debug fruit | first .length is 6      // Banana
          
          

      'last':
        subtitle 'last value'
        summary '''
          Returns the last value in a list or iterator. With an expression, block or function, returns the
          last value where the expression is true.
        detail '''
          If the collection is an iterator, it will be exhausted. If it's an infinite iterator, the machine will stop
          responding.

              debug fruit | last
  
              > Citron

          __Last__ doesn't just crop the array, it unwraps whatever value is there, as 
          if you'd coded `array.[array.length-1]`.  __Last__ unwraps iterators, returning a single
          value (or undefined). If you need the iteration chain preserved, use __limit__ (in combination with
          __has__ if necessary).

          #### Last with a comparison
          
          If you provide a comparison expression, block, or function, __last__ will evaluate the comparison
          against each element in the collection, and return the last value that the comparison returns true.

          Example:
          
              set fruit list Apple, Banana, Cherry, Durian
          
              debug fruit | last .length is 6      // Durian
              
          #### Quirks
          
          Arrays: __last__ with comparison starts at the tail of the array, for performance reasons.
          
          Objects: since the keys of an object have no defined order, both __last__ and __first__ 
          examine objects the same way -- unpredictably.

          Iterators: __last__ starts at the beginning, compares every item, and returns the last one found
          when the iterator ends. If the iterator doesn't end, __last__ will hang. If no match is found,
          __last__ does not yield a value, it simply terminates. If your comparison is expensive, you may
          wish to __collect__ the iterator (turning it into an array) before applying __last__; this exchanges 
          memory use for processing speed.


      'count':
        subtitle 'collection item count'
        summary '''
          Returns the number of elements in the collection. 
        detail '''
          If the collection is an iterator, it will be drained.

              debug undefined count  
              debug 'apples' count 
              debug fruit count
   
              > 0
              > 1
              > 3

          Count will count arrays, enumerable keys in objects, iterators, values (count: 1), 
          and `undefined` (count: 0).

      'by':
        subtitle 'collection sorting'
        summary '''
          To create a sorted array, use **by**. The newly resulting array will be sorted by the specified 
          inline expression or code block. 
        detail '''
          **By** does not sort in-place; it always returns a new array.
          If **by** is used on an iterable, it will **collect** all values before sorting.
          If **by** is used on a true object, it will be converted to an array.

          #### by inline

          When you use the inline expression form, the provided expression is used to extract a 
          value from a single record, which is then compared against other records to determine the sort order.

          The **it** pronoun and its **dot** scoping prefix are available within the expression.

          Print a list of friends sorted alphabetically by name:

              set result to friends | by .name
  
              debug ( result | thru .name ).join(', ')
  
              > Ann, Doug, Ellie, Harry, Jenna, John, Marshal, Sara 

          You may sort by more than one value by adding another **by** clause. Multiple sort clauses 
          are handled the way a database would: if a clause provides no guidance (the values are 
          identical), subsequent clauses are each checked in turn.

          Print sorted by length of name, then age:

              set result to friends | by .name.length by .age
  
              debug (result | thru '$\{.name}: $\{.age}').join('\\n')
  
              > Ann: 23
              > Doug: 18
              > John: 19
              > Sara: 23
              > Ellie: 22
              > Jenna: 28
              > Harry: 31
              > Marshal: 21

          #### Sorting of objects by the property/key
          
          In _inline_ expression sorting only, if your source collection is an object, you can use the __key__ 
          pronoun as sort criteria, in addition to the __it__ pronoun and related accessors. The key is the 
          property name. In the example below, `rejections` is one of the keys in the folder object, 
          while `34` is its value.
          
              set folder:
                rejections 34
                acceptances 23
                pending 4
              debug folder |by key

              > [ 4, 23, 34 ]
              
          As you can see, the key is discarded in the final array output.  If you need
          different behaviour, you'll need to partition the task into simpler components, e.g. by using
          __enlist__, __by__ and __thru__ in turn to preserve the data in the form you need.

          Using __key__ as sort criteria on something that isn't an object will throw a runtime exception.

          #### by block

          When using **by** with a block of code, the pronouns change. You are given both records 
          under consideration just as you would using the Javascript `.sort` method. And 
          perhaps unsurprisingly to those who have ever seen a programming textbook, the pronouns 
          are the letters **a** and **b**.

          Print a sorted list of ages by province:

              set result to friends | by
                return a.province <=> b.province or a.age <=> b.age
  
              debug (result |thru '$\{.name}: $\{.province}, $\{.age}').join('\n')
  
              > Doug: ON, 18
              > John: ON, 19
              > Marshal: ON, 21
              > Sara: ON, 23
              > Jenna: ON, 28
              > Ellie: QC, 22
              > Ann: QC, 23
              > Harry: QC, 31

          Note the __key__ pronoun is not available with block expression sorting.

          #### by using

          __By using__ provides the ability to use a named function for your 
          sorting facilitator.

              set ProvinceAge to task given a, b
               return a.province <=> b.province or a.age <=> b.age
  
              set result to friends | by using ProvinceAge
  
              // same result as previous example

          #### desc (descending) _modifier_

              .. [expr] | by [expr] (desc)  

          Used with the **by** comprehension just like **asc** but the sort order will be highest to lowest.

              debug list Bob, Carol, Ted, Alice | by it desc
  
              > [ 'Ted', 'Carol', 'Bob', 'Alice' ]


      'has':
        subtitle 'collection filtering'
        summary '''
          Use __has__ to test for exclusion each object in a collection.
        detail '''
          The pipe operator **has** indicates an expression based filter using the **it** 
          _pronoun_ to represent the item currently under examination. Since **it** is in play, 
          the associated **dot** scoping prefix is also active within the expression for 
          easy access to item fields. 

          #### has inline

              debug friends | has (.province = 'QC') and (.cat or .dog)
  
              > [ { name: 'Ann', age: 23, cat: true, province: 'QC' } ]
 
          Any valid expression can be used in a **has** piper, and one can refer 
          to values outside the expression as well.

              set rentAge to: ON 25, QC 21
              debug friends | has .age >= rentAge[.province]
  
              > [ { name: 'Ellie', age: 22, province: 'QC' },
              >   { name: 'Ann', age: 23, cat: true, province: 'QC' },
              >   { name: 'Harry', age: 31, province: 'QC' },
              >   { name: 'Jenna', age: 28, dog: true, province: 'ON' } ]
 
          #### has block

          **Has** can reference a block of code directly, which makes 
          the **it** pronoun available within that block, or be given the name of a 
          task (or a task definition). 
          
          You must **return** a _truthy_ (keep) or 
          _falsy_ (discard) value from the block so the filter can take the according 
          action. If you don’t return a value, all rows will be discarded.

              friends | has
                return .age >= rentAge[.province]

          #### has using

          You can integrate function calls by adding the 
          **using** keyword immediately following the __has__ keyword itself.

              set CanRent to task given row
                return row.age >= rentAge[row.province]

              debug friends | has using CanRent


        
      'lowest':
        subtitle 'search collection for lowest value'
        summary '''
          Returns the collection element with the lowest value of the expression. 
        detail '''
          Prepares the pronouns **it** and **key** for use by the expression.

              debug friends lowest .age

              > { name: 'Doug', age: 18, province: 'ON' }

          __Lowest__ is like a combination of __by__ and __first__, except that instead of
          sorting, it just runs through the collection and finds the correct answer. (This is faster.)
        
        
      'highest':
        subtitle 'search collection for highest value'
        summary '''
          Returns the collection element with the highest value of the expression. 
        detail '''
          Prepares the pronouns **it** and **key** for use by the expression.

              debug friends highest .age
  
              > { name: 'Harry', age: 31, province: 'QC' }

          __Highest__ is like a combination of __by__ and __first__, except that instead of
          sorting, it just runs through the collection and finds the correct answer. (This is faster.)

